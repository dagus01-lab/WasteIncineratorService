/** Modello per l'architettura logica dello SPRINT2 del WasteIncineratorService */

System waste_incinerator_service

//Messaggio per la ricezione di un nuovo RP
Dispatch arrived_RP : arrived_RP(N) "command that simulate the arrival of new RP"

//Messaggio per notificare lo stato del Incinerator al WIS
Dispatch statoIncinerator : statoIncinerator(N)  "Incinerator's burning status info: 0 is not burning, 1 otherwise"

//Messaggi per la gestione del burning dell'Incinerator
Dispatch activationCommand : activationCommand(N) "command that turns the incinerator on"
Dispatch startBurning : startBurning(N) "command that makes the incinerator start burning the next RP"
Dispatch rpInBurnin : rpInBurnin(N)  "command that makes the wis start the incinerator"
Event 	 endBurning : endBurning(N) "event generated by the incinerator when the burning process is terminated"

Event alarm : alarm(X)

//Messaggio per notificare l'aggiunta di nuova cenere.
Dispatch newAshes : newAshes(N) "command that simulate the arrival of new RP's ashes"
 
//Messaggio per notificare lo stato del AshStorage al WIS
Event statoAshStorage : statoAshStorage(N)  "AshStorage info: 0 is not empty, 1 otherwise"

//Messaggi per ingaggiare e muovere il BasicRobot
Request engage        : engage(OWNER, STEPTIME)	
Reply   engagedone    : engagedone(ARG)    for engage
Reply   engagerefused : engagerefused(ARG) for engage
Dispatch disengage    : disengage(ARG)

Request moverobot    :  moverobot(TARGETX, TARGETY)   
Reply moverobotdone  :  moverobotok(ARG)                    for moverobot
Reply moverobotfailed:  moverobotfailed(PLANDONE, PLANTODO) for moverobot

//Messaggi per la gestione dei test
Request info: info(X)
Reply inforeply: inforeply(X) for info

//Definizione dei contesti
Context ctx_waste_incinerator_service ip [host="localhost" port=8125]
Context ctxbasicrobot ip [host="127.0.0.1" port=8020] 
Context ctxmonitoringdevice ip [host="192.168.1.105" port=8100]
//Context ctxscale ip [host="192.168.1.105" port=8101]

//Attori esterni al WIS
ExternalQActor basicrobot context ctxbasicrobot
ExternalQActor monitoringdevice context ctxmonitoringdevice
//ExternalQActor scale context ctxscale

//--------------------------------Attore WIS-----------------------------------------------------------------
QActor wis context ctx_waste_incinerator_service{
	[# var AshStorageStatus = 0 #]
	
	State s0 initial{
		delay 500
		println("$name STARTS") color green
		connectToMqttBroker "wss://test.mosquitto.org:8081" clientid "wisnat"
 		println("$name | CREATED  (and connected to mosquitto) ... ") color green
 		subscribe "unibodisi"
		//si mette ad osservare lo stato del AshStorage fornito dal MonitoringDevice
		//observeResource monitoringdevice msgid statoAshStorage
		//mando messaggio di attivazione del Incinerator
		forward incinerator -m activationCommand : activationCommand(1)
	}
	Goto waitingRP
	
	//Stato che simula l'attesa di un nuovo RP nel WasteStorage
	State waitingRP{
	}
	Transition t0 whenEvent statoAshStorage -> handleUpdateStatoAshStorage
				  whenMsg arrived_RP ->  handleRP
				
	//Stato per gestire l'arrivo di un nuovo RP nel WasteStorage
	State handleRP{
		println("New RP is arrived") color green
		//notifico il robot che un nuovo RP è disponibile
		forward oprobot -m arrived_RP : arrived_RP(1)
	}
	Transition t0 whenMsg rpInBurnin -> handleRPInBurnin
	
	//Stato per gestire l'arrivo del opRobot nella porta di BURNIN del Incinerator
	State handleRPInBurnin{
		println("An RP is ready to be burnt") color green
		//mando messaggio di inizio burning al Incinerator
		forward incinerator -m startBurning : startBurning(1)
	}
	Transition t0 whenEvent endBurning -> handleEndBurning
	
	//Stato per gestire l'evento di endBurning
	State handleEndBurning{
		println("Incinerator has finished to burn") color green
	}
	Transition t0 whenMsg newAshes -> endRoute
	
	//Stato per gestire la "fine del giro" di scarico del robot
	State endRoute{
		println("$name OpRobot has finished its route") color green
		//se entro 1 secondo non ricevo aggiornamenti sullo stato del AshStorage, 
		//allora considero di poter bruciare un nuovo RP
	}
	Transition t0 whenTime 1000 -> waitingRP
				  whenEvent statoAshStorage -> handleUpdateStatoAshStorage
							
	//Stato per gestire l'aggiornamento dello stato del AshStorage
	State handleUpdateStatoAshStorage{
		printCurrentMessage color green
		onMsg( statoAshStorage : statoAshStorage(N)){
			println("payloadArg: ${payloadArg(0)}") color green
			[# AshStorageStatus = payloadArg(0).toInt() #]
			println("AshStorageStatus: $AshStorageStatus") color green
		}
	}
	Goto waitingRP if [# AshStorageStatus == 0 #] else waitingAshesToBeRemoved
	
	//Stato per gestire l'attesa che la cenere nel AshStorage sia rimossa per poter bruciare nuovi RPs
	State waitingAshesToBeRemoved{
		println("WIS is waiting an operator to remove ashes in AshStorage...") color green
	}
	Transition t0 whenEvent statoAshStorage -> handleUpdateStatoAshStorage
}
//-----------------------------------------------------------------------------------------------------------

//----------------------------------------Attore OpRobot-----------------------------------------------------
QActor oprobot context ctx_waste_incinerator_service{
	[#	
		var HOMEx = 0
		var HOMEy = 0
		var WASTEINx = 0
		var WASTEINy = 4
		var BURNINx = 3
		var BURNINy = 2
		var BURNOUTx = 4
		var BURNOUTy = 3
		var ASHOUTx = 6
		var ASHOUTy = 4
	#]
	State s0 initial{
		println("$name STARTS") color yellow
	}
	Goto engage
	
	//Stato per provare ad "ingaggiare" il BasicRobot
	State engage{
		println("$name | $MyName engaging ... ") color yellow 
		request basicrobot -m engage : engage($MyName,330) //pc Emanuele 112
	}
	Transition t0 whenTime 1000 -> noResponse
				  whenReply engagedone   -> waitingWorking
 	              whenReply engagerefused -> end
 	          
 	//stato che rappresenta l'assenza di risposta da parte del basicrobot
 	State noResponse{
 		println("BasicRobot did not answer!") color yellow
 	}
 	Goto end
	
	//Stato che rappresenta il robot in attesa di lavoro
	State waitingWorking{
		println("$name is in HOME position waiting for working") color yellow
	}
	Transition t0 whenMsg arrived_RP -> takeRP
	
	//Stato che rappresenta il robot che va a recuperare un RP dal WasteStorage
	State takeRP{
		println("OpRobot is going to take an RP..") color yellow
		request basicrobot -m moverobot:moverobot($WASTEINx,$WASTEINy)
	}
	Transition t0 whenReply moverobotdone -> bringRPtoBURNIN
				  whenReply moverobotfailed -> exit
				  
	//Stato che rappresenta il robot che porta l'RP recuperato dal WasteStorage nella porta BURNIN
	State bringRPtoBURNIN{
		delay 2000 //simulazione del prelievo di un RP dal WasteStorage
		request basicrobot -m moverobot:moverobot($BURNINx, $BURNINy)
	}
	Transition t0 whenReply moverobotdone -> returnHOME
				  whenReply moverobotfailed -> exit
				  
	//Stato che rappresenta lo spostamento in HOME del robottino una volta che l'RP è stato messo nell'inceneritore
	State returnHOME{
		delay 2000 //simulazione del rilascio di un RP in BURNIN
		forward wis -m rpInBurnin : rpInBurnin(1)
		println("An RP is in BURNIN port") color yellow
		request basicrobot -m moverobot:moverobot($HOMEx, $HOMEy)
		//then, return HOME
	}
	Transition t0 whenReply moverobotdone -> waitingForIncinerator
				  whenReply moverobotfailed -> exit
				 
	//Stato che rappresenta l'attesa del incinerator che sta bruciando
	State waitingForIncinerator{
		println("Waiting for incinerator to finish its job") color yellow
	}
	Transition t0 whenEvent endBurning -> takeAshFromBURNOUT
	
	//Stato che rappresenta il robot che preleva la cenere dalla porta BURNOUT
	State takeAshFromBURNOUT{
		request basicrobot -m moverobot:moverobot($BURNOUTx, $BURNOUTy)
	}
	Transition t0 whenReply moverobotdone -> bringAshtoASHOUT
				  whenReply moverobotfailed -> exit
				  
	//Stato che rappresenta il robot che va a depositare la cenere raccolta dall'inceneritore
	State bringAshtoASHOUT{
		delay 2000 //simulo il prelievo della cenere da BURNOUT
		request basicrobot -m moverobot:moverobot($ASHOUTx, $ASHOUTy)
	}
	Transition t0 whenReply moverobotdone -> restartJob 
				  whenReply moverobotfailed -> exit
				  
	//Stato che rappresenta il robot che torna in HOME dopo aver scaricato le ceneri nell'AshStorage
	State restartJob{
		delay 2000 //simulo lo scaricamento della cenere
		println("The ash has been taken out") color yellow
		//mando notifica di nuova cenere
		forward wis -m newAshes : newAshes(1)
		request basicrobot -m moverobot:moverobot($HOMEx, $HOMEy)    
	}
	Transition t0 whenMsg arrived_RP -> takeRpBeforeFinishPlan
				  whenReply moverobotdone -> waitingWorking
				  whenReply moverobotfailed -> exit
	
	State takeRpBeforeFinishPlan{
		println("A new RP has arrived before OpRobot returned HOME") color yellow
		delay 500
		emit alarm:alarm(1) 
	}
	Transition t0 whenReply moverobotdone -> testko
				  whenReply moverobotfailed -> takeRP
	
	State testko{
		println("BasicRobot ignored alarm!!") color yellow
	}
	Goto takeRP
	
	//Stato di fine, nel caso l'engage del BasicRobot non fosse andata a buon fine
	State end{
		println("$name | ENDS ") color yellow
	}
	
	State exit{
		println("$name has been terminated") color yellow
	}
}
//-----------------------------------------------------------------------------------------------------------

//---------------------------------Attore Incinerator--------------------------------------------------------
QActor incinerator context ctx_waste_incinerator_service{
	[# val BTIME = 10000L; var stato = 0; #]
	
	State s0 initial{
		println("$name STARTS") color red
	}
	Transition t0 whenMsg activationCommand -> turnOn
	
	State turnOn {
		println("Incinerator is on. Waiting for RPs to burn...") color red
	}
	Transition t0 whenMsg startBurning -> handleStartBurning
	
	//Stato per gestire l'inizio di burning
	State handleStartBurning{
		println("Incinerator is burning...") color red
		//updateResource [# "statoIncinerator(1)" #] //aggiorno gli Observer collegati
		forward monitoringdevice -m statoIncinerator:statoIncinerator(1)
	}
	Transition t0 whenTimeVar BTIME -> handleEndBurning
	
	//Stato per gestire la fine di burning
	State handleEndBurning{
		emit endBurning : endBurning(1) //emette il segnale di fine burning
		//updateResource [# "statoIncinerator(0)" #]
		forward monitoringdevice -m statoIncinerator:statoIncinerator(0)
	}
	Transition t0 whenMsg startBurning -> handleStartBurning   
}
//-----------------------------------------------------------------------------------------------------------

//---------------------------------Attore WisTester----------------------------------------------------------
QActor wistester context ctx_waste_incinerator_service{
	State s0 initial{
		println("$name STARTS") color blue
	}
	Transition t0 whenRequest info -> handleinfo
	
	//Stato che rappresenta l'arrivo di una richiesta di test da parte di Junit
	State handleinfo{
		println("$name: new test request arrived") color blue
		forward wis -m arrived_RP : arrived_RP(1)
	}
	Transition t0 	whenTime 70000 -> failure
					whenMsg newAshes -> success
					
	State success {
		replyTo info with inforeply:inforeply(1)
	}
	Transition t0 whenRequest info -> handleinfo
	
	State failure {
		replyTo info with inforeply:inforeply(0)
	}
	Transition t0 whenRequest info -> handleinfo
	
				  
}
//-----------------------------------------------------------------------------------------------------------


//-----------------------Attore mock che simula il comportamento della Scale---------------------------------
QActor scalemock context ctx_waste_incinerator_service{
	State s0 initial {
		println("$name STARTS") color black
	}
	Transition t0 whenTime 1000 -> simulateRP //l'attore simula l'arrivo di un RP dopo 1 secondo
	
	State simulateRP {
		delay 5000
		forward wis -m arrived_RP : arrived_RP(1)
		//delay 38000
		//forward wis -m arrived_RP : arrived_RP(1)
	}
	Goto simulateRP
}
//-----------------------------------------------------------------------------------------------------------
/*
//------------------------Attore mok che simula il comportamento del MonitoringDevice------------------------
QActor monitoring_device_mok context ctx_waste_incinerator_service{
	[# var ashLevel = 3; val MAX_LEVEL = 4 #] //livello delle cenere sotto forma di numero di RP bruciati
	
	State s0 initial{
		println("$name STARTS") color yellow
	}
	Transition t0 whenMsg newAshes -> handleAshes
	
	//Stato per gestire l'arrivo di nuova cenere nel container
	State handleAshes{
		[# ashLevel ++ #]
		//Se il livello della cenere = MAX_LEVEL, allora non posso bruciare altri RP
		if [# ashLevel == MAX_LEVEL #]
		{
			println("AshStorage is full. It is not possible to burn new RPs.") color yellow
			updateResource [# "statoAshStorage(1)" #] //aggiorno gli Observer collegati
		}
		else
		{
			updateResource [# "statoAshStorage(0)" #] //aggiorno gli Observer collegati
		}
	}
	Transition t0 whenTime 40000   -> simulateRemoveAshes
				  whenMsg newAshes -> handleAshes
				  
	//Stato per simulare la svuotamento del container di cenere
	State simulateRemoveAshes{
		println("Removed ashes from AshStorage. Now the container is empty.") color yellow
		[# ashLevel = 0 #]
		updateResource [# "statoAshStorage(0)" #] //aggiorno gli Observer collegati
	}
	Transition t0 whenMsg newAshes -> handleAshes
}
//-----------------------------------------------------------------------------------------------------------
*/
