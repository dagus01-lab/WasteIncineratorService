/** Modello per l'architettura logica dello SPRINT2 della scale */

System scale

//Messaggi inerenti lo stato del WasteStorage
Event scaledata : weight(W)   "emitted  by scale or (better) by datacleaner"
Event wasteStorageRPs:wasteStorageRPs(N)   "emitted  by datacleaner"
Event new_RP : new_RP(N) "command that simulate the arrival of new RP"

//Definizione dei contesti
Context ctxscale ip [ host= "localhost"      port= 8200 ] 

//--------------------------------Attore scaledevice------------------------------------------------------
QActor scaledevice context ctxscale{
[# 
	lateinit var reader : java.io.BufferedReader
    lateinit var p : Process	
    var Weight = 0
    var WeightMAX = 500
#]	
	
	State s0 initial{
		println("$name | scalestart") 
	 	[#
			p       = Runtime.getRuntime().exec("python scaleDevice.py")
			reader  = java.io.BufferedReader(  java.io.InputStreamReader(p.getInputStream() ))	
		#]		
	}
	Goto readscaleData
	
	//Stato per la lettura dei dati dalla scale fisica
	State readscaleData{
	[# 
		var data = reader.readLine()
		//CommUtils.outyellow("$name with python: data = $data"   ) 
		if( data != null ){
			try{ 
				val vd = data.toFloat()
				val v  = vd.toInt()
				if( v <= WeightMAX ){	
					Weight = v				
				}else Weight = WeightMAX
			}catch(e: Exception){
					CommUtils.outred("$name readscaleDataERROR: $e "   )
			}
		}
		
	#]	
		if [# Weight > 0 #] { 
		    //println("$name with python: data = $Weight"   ) color yellow
			emitlocalstream scaledata : weight($Weight)			 
		}
		//autodispatch doread : doread(1)
	}
	Goto readscaleData
}
//--------------------------------------------------------------------------------------------------------

//--------------------------------Attore scale------------------------------------------------------------
QActor scale context ctxscale { 
	[# 
		var RPs = 0;
		var previous_RPs = 0; 
		val WRP = 100;
		var timeLastUpdate = System.currentTimeMillis();
		var timeout = 10000;
	#]
	
	State s0 initial {
		delay 1000
		subscribeTo scaledevice for scaledata		
		println("$name subscribed to scaledevice") color blue
		connectToMqttBroker "tcp://localhost:8081" clientid "scalenat"
		//connectToMqttBroker "tcp://test.mosquitto.org" clientid "scalenat"
 		println("$name | CREATED  (and connected to mosquitto) ... ") color blue
 		subscribe "wisinfo"
 		publish "wisinfo" -m new_RP:new_RP(0)
	}
	Transition t0 whenEvent scaledata -> filter
	
	//Stato per gestire i dati emessi dalla scaledata
	State filter {
     	onMsg(scaledata : weight(W)) {
	      	[#  
	      		var W = payloadArg(0).toInt() 
	      		RPs = (W/WRP).toInt()
	      	#]	      		      	
	      	//Se il numero di RPs Ã¨ aumentato, notifico tale info al WIS
	      	if [#RPs > previous_RPs#]{ //verificare il margine della misura
	      		println("$name emit number of RPs=$RPs in Waste Storage") color magenta
	      		[#
	      			for (i in previous_RPs..RPs-1) {
	      				#]
	      				publish "wisinfo" -m new_RP:new_RP(1)
	      				[#
	      			}
	      			timeLastUpdate = System.currentTimeMillis()
	      		#]
	     	} 
	     	if [#System.currentTimeMillis()-timeLastUpdate>=10000#]{
	     		publish "wisinfo" -m new_RP:new_RP(0)
	     		[#timeLastUpdate=System.currentTimeMillis()#]
	     	}
	      	[# previous_RPs = RPs; #]
     	}
	}	
	Transition t0 whenEvent scaledata -> filter
}
//--------------------------------------------------------------------------------------------------------
